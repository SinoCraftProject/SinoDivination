plugins {
    id 'eclipse'
    id 'maven-publish'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

apply plugin: 'java'
apply plugin: 'architectury-plugin'   // Parchment mappings.
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'
apply plugin: 'dev.architectury.loom'

group = 'games.moegirl.sinocraft.sinodivination' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'sinodivination'

version = '1.18.2-2.0.0'
// qyl27: for developing and release.
if (System.getenv("JITPACK") == "true") {
    version += "-${System.getenv("GIT_BRANCH")}"
    version += "-${System.getenv("GIT_COMMIT")}"
}

var release = false
if (release || project.hasProperty("SINOSERIES_RELEASE")) {
    version += '-release'
} else {
    version += '-dev'
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

println("Building SinoDivination. Ver: $version.")

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    silentMojangMappingsLicense()

    forge {
        dataGen {
            mod 'sinodivination'
        }
        mixinConfig "sinodivination.mixins.json"
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    maven { url 'https://jitpack.io' }

    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }

    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    minecraft "com.mojang:minecraft:1.18.2"
    forge "net.minecraftforge:forge:1.18.2-40.1.0"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.18.2:2022.05.22@zip")
    }

    modCompileOnly 'com.github.SinoCraftProject:SinoCore:dev-SNAPSHOT:api'
    modRuntimeOnly 'com.github.SinoCraftProject:SinoCore:dev-SNAPSHOT'
    // qyl27: Remove JEI because it was inherited from SinoCore.
    modImplementation 'curse.maven:the-one-probe-245211:3671753'
    modRuntimeOnly 'curse.maven:scannable-266784:3738448'
    modRuntimeOnly 'curse.maven:appleskin-248787:3686482'
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title"     : "sinodivination",
                "Specification-Vendor"    : "SinoCraft Project Team",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "MoeGirl Games",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "sinodivination.mixins.json"
        ])
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task injectString(type: Copy) {
    from ('src/main/java') {
        include '**/*.java'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
    into "${buildDir}/generated/java"
}

// qyl27: It only works with main.
compileJava.source = "${buildDir}/generated/java"
compileJava.dependsOn injectString
